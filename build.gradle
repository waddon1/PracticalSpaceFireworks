plugins {
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.+'  // This needs to be first
    id 'org.spongepowered.mixin' version '0.7.+'       // This should come after
}

group = maven_group
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: 'official', version: minecraft_version
    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }
        
        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }
        
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url = 'https://maven.neoforged.net/releases' }
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url = 'https://maven.minecraftforge.net/' }
    maven { url = 'https://modmaven.dev/' }
    maven { 
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url "https://cursemaven.com"
    }
    flatDir { dirs 'libs' }
}

dependencies {
    minecraft "net.neoforged:neoforge:${minecraft_version}-${neoforge_version}"

    // JEI
    implementation fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version,
            neoforge_version: neoforge_version,
            loader_version: loader_version,
            mod_id: mod_id,
            mod_version: mod_version,
            mod_author: mod_author
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title": mod_id,
                "Specification-Version": "1",
                "Specification-Vendor": mod_author,
                "Implementation-Title": project.name,
                "Implementation-Version": project.jar.archiveVersion,
                "Implementation-Vendor": mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}